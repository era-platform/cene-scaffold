\= util.cene (part of Cene Scaffold)
\= Copyright 2016 Ross Angle. Released under the MIT License.

(defn fix func
  (fn arg
    (c func (c-new fix func) arg)))

(defn strings-append-later strings then
  (foldl-later str.\;qq[] strings
    (fn state elem then /string-append-later state elem then)
    then))

(def-macro istr unique-ns definition-ns my-stx-details args then
  (basic-pure-macro unique-ns definition-ns my-stx-details then
  /fn unique-ns s mac
  /cast args cons istr args
    err.\;qq[Called istr without enough arguments]
  /cast args nil
    err.\;qq[Called istr with too many arguments]
  /cast istr stx - istr
    err.\;qq[Called istr with an argument that wasn't a stx]
  /let loop
    (fix/fn loop istr
      (case istr
        
        istring-cons prefix interpolation rest
        (cons (c s /c mac str.str /list/c s istring-nil.prefix)
        /cons interpolation
        /c loop rest)
        
        istring-nil suffix
        (list/c s /c mac str.str /list/c s istring-nil.suffix)
        
        err.\;qq[
          Called istr with a value that wasn't an interpolated
          string]))
  /c s /c mac str.strings-append-later /list/c s /c mac str.list
  /c loop istr))

(defn copy-paths mode in out
  (case (input-path-type mode in)
    
    file-type-directory
    (foldr (input-path-directory-list mode in) (no-effects/nil)
    /fn item then
      (join-effects
        (c-new copy-paths mode
          (input-path-get in item)
          (output-path-get out item))
        then))
    
    file-type-blob
    (output-path-blob-utf-8 out /input-path-blob-utf-8 mode in)
    
  /no-effects/nil))

(defn make-quine mode definition-ns out string-name top-level-vars
  (output-path-blob-utf-8 out
  /sloppy-javascript-quine mode
    (procure-name mode /ns-path definition-ns /list
      str.constructors
      (procure-name mode /ns-path definition-ns /list
        str.constructor-names string-name str.name)
      str.tag)
    top-level-vars))

(defn compile-give-unwrapped-js-effects param js-val body
  (then-js-effects
    (compile-function-js-effects (cons param /nil) body)
  /fn body
  /give-unwrapped-js-effects js-val body))
